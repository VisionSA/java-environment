<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" 
		   layout="vertical" width="100%" height="100%" 
		   xmlns:customComponent="com.bizit.consulta.web.view.customComponent.*"
		   xmlns:retenciones="com.bizit.consulta.web.view.customComponent.retenciones.*"
		   currentState="{modelo.estadoBuscarCertRetComercio}"
		   initialize="module_initializeHandler(event)">

	<fx:Declarations>
		<mx:DateValidator id="vFechaValidator" source="{dfFechaFiel}" property="selectedDate"
						  required="true" requiredFieldError="La fecha desde es obligatoria"/>
		<mx:CurrencyFormatter id="cfMonedaFormatter" alignSymbol="left" currencySymbol="$" 
							  decimalSeparatorFrom="." decimalSeparatorTo=","
							  thousandsSeparatorFrom="" thousandsSeparatorTo="."
							  precision="2" useNegativeSign="true" useThousandsSeparator="true"/>
		<mx:DateFormatter id="dfFechaFormatter" formatString="DD/MM/YYYY"/>
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import com.bizit.consulta.web.event.ComercioEvent;
			import com.bizit.consulta.web.model.FielWebModelLocator;
			import com.bizit.consulta.web.utils.ConstantesEstados;
			import com.bizit.consulta.web.utils.ManejadorMensajes;
			import com.bizit.consulta.web.utils.ManejadorPantallas;
			import com.bizit.consulta.web.view.module.ViewConsultaComercioModule;
			import com.bizit.consulta.web.view.renderer.ArchivosPdfLiquidacionRenderer;
			import com.bizit.consulta.web.vo.SetDataVo;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ValidationResultEvent;
			
			[Bindable]
			private var modelo:FielWebModelLocator = FielWebModelLocator.getInstance();
			
			private var nombreArchivo:String = null;
			
			protected function btnBuscar_clickHandler(event:MouseEvent):void
			{
				if (filtroValido())
				{
					ManejadorPantallas.mostrarProgressBar();
					dgRetenciones.invalidateDisplayList();
					new ComercioEvent(ComercioEvent.BUSCAR_CERT_RET_COMERCIO_EVENT, dfFechaFiel.selectedDate).dispatch();
					//modelo.estadoBuscarCertRetComercio = ConstantesEstados.RESULTADOS_CERT_RET_COMERCIO_STATE;
				}
			}
			
			private function filtroValido():Boolean
			{
				var resEv:ValidationResultEvent = vFechaValidator.validate();
				if (resEv.type == ValidationResultEvent.INVALID)
				{
					return false;
				}
				return true;
			}
			
			
			private function btnCerrar_clickHandler(event:MouseEvent):void
			{
				new SetDataVo("listaRetComercio", new ArrayCollection()).guardarValor();
				ArrayCollection(dgRetenciones.dataProvider).refresh();
				dgRetenciones.invalidateDisplayList();
				dgRetenciones.validateNow();
				dfFechaFiel.selectedDate = null;
				new SetDataVo("estadoBuscarCertRetComercio", ConstantesEstados.BUSCAR_CERT_RET_COMERCIO_STATE).guardarValor();
				ViewConsultaComercioModule(parentDocument).unloadContentModule(); 
			}
			
			private function grid_labelFunc_Periodos(item:Object, column:AdvancedDataGridColumn):String 
			{
				dgRetenciones.invalidateDisplayList();
				return dfFechaFormatter.format(item.perDesde) + " - " + dfFechaFormatter.format(item.perHasta);
			}
			
			protected function sttGuardar_enterStateHandler(event:FlexEvent):void
			{
				ManejadorMensajes.mostrarMensajeOkCancel("Â¿Desea guardar el archivo " + this.nombreArchivo + "?", doAction);			
			}
			
			private function doAction(eventObj:CloseEvent):void
			{
				if (eventObj.detail==Alert.OK)
				{
					var pdfFile:FileReference = new FileReference();
					pdfFile.save(modelo.byteArrayPdf, nombreArchivo + ".pdf");
				}
			}
			
			public function setNombreArchivoPdf(nombre:String):void
			{
				if (nombre != null)
				{
					this.nombreArchivo = nombre.substr(nombre.lastIndexOf("/")+1, nombre.length - (nombre.lastIndexOf("/")+1));
				}
			}

			protected function module_initializeHandler(event:FlexEvent):void
			{
				new SetDataVo("estadoBuscarCertRetComercio", ConstantesEstados.BUSCAR_CERT_RET_COMERCIO_STATE).guardarValor();
			}
			
			private function ordenarPeriodo(obj1:Object, obj2:Object):int
			{
				var per1:Array = obj1.periodo.toString().split("/");
				var date1:Date = new Date(per1[1], per1[0]);
				
				var per2:Array = obj2.periodo.toString().split("/");
				var date2:Date = new Date(per2[1], per2[0]);
				
				var d1:Number = date1.getTime();
				var d2:Number = date2.getTime();
				if(d1 < d2) {
					return -1;
				} else if(d1 == d2) {
					return 0;
				}
				return 1;
			}

		]]>
	</fx:Script>
	
	<mx:states>
		<s:State name="buscarCertRetState" id="sttBuscar"/>
		<s:State name="resultadosCertRetState" id="sttResultados"/>
		<s:State name="guardarArchivoState" id="sttGuardar" enterState="sttGuardar_enterStateHandler(event)"/>
	</mx:states>
	
	
	<s:VGroup width="100%" height="100%" horizontalAlign="center">
		<s:BorderContainer width="100%"  backgroundColor="#A2C1FF" backgroundAlpha="0.31" height="35">
			<s:layout>
				<s:HorizontalLayout verticalAlign="middle" paddingLeft="5" paddingRight="5" paddingTop="5" paddingBottom="5"/>
			</s:layout>
			<s:Label id="lblFecha" text="Fecha desde: "/>
			<customComponent:FielDateField id="dfFechaFiel"/>
			<s:Button id="btnBuscar" label="Buscar" click="btnBuscar_clickHandler(event)"/>
			<s:HGroup width="100%" horizontalAlign="right">
				<mx:LinkButton id="btnCerrar" label="Cerrar" click="btnCerrar_clickHandler(event)"/>
			</s:HGroup>
		</s:BorderContainer>

		<mx:AdvancedDataGrid id="dgRetenciones" dataProvider="{modelo.listaRetComercio}"
					width="100%" height="100%" 
					visible="true" includeInLayout="true"
					visible.buscarCertRetState="false" includeInLayout.buscarCertRetState="false"
					visible.resultadosCertRetState="true" includeInLayout.resultadosCertRetState="true"
					alternatingItemColors="{['#cee2ff','#84b5ff']}" verticalAlign="middle"
					variableRowHeight="true"
					headerRenderer="com.bizit.consulta.web.view.renderer.GridHeaderRenderer"
					rollOverColor="0x007AC0" textRollOverColor="white" selectionColor="0x007AC0"
					textSelectedColor="white">
			<mx:columns>
				<mx:AdvancedDataGridColumn headerText="Periodo" dataField="periodo" textAlign="center" width="70"
										   sortCompareFunction="ordenarPeriodo"/>
<!--@I4923-->				<mx:AdvancedDataGridColumn headerText="Codigo Posnet" dataField="codigoPosnet" textAlign="center" width="50"/>
<!--@F4923-->	<mx:AdvancedDataGridColumn headerText="PDF" itemRenderer="com.bizit.consulta.web.view.renderer.ArchivosPdfLiquidacionRenderer" 
										   width="70" sortable="false"/>
				<mx:AdvancedDataGridColumn headerText="Detalle" 
										   itemRenderer="com.bizit.consulta.web.view.renderer.RetencionesDetallesRenderer" 
										   textAlign="center" sortable="false"/>
			</mx:columns>			
		</mx:AdvancedDataGrid>
	</s:VGroup>
</mx:Module>
